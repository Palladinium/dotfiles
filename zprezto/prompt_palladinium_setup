function prompt_palladinium_precmd {
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_palladinium_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_palladinium_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '' \
    'rprompt' ' %F{green}(%f$(coalesce "%b" "%p" "%c")%C%D%s%F{green})%f'

  # Colors:
  # Red:    160
  # Pink:   200
  # Yellow: 226
  
  # Define prompts

  local user='%n%F{160}@%f%F{200}%m%f'
  local pwd='%F{green}%3~%f'
  local git_left='${(e)git_info[prompt]}'
  local dollar='%F{blue}$%f'
  PROMPT="${user} ${pwd}${git_left} ${dollar} "
  
  local git_right='${(e)git_info[rprompt]}'
  local return_code='%(?..%F{160}%?<%f)'
  RPROMPT="${return_code}${git_right}"
}

prompt_palladinium_setup "$@"
